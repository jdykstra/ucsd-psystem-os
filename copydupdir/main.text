(*
 * UCSD PASCAL - Version I.5
 * Copyright (C) 1978, 1979 Regents of the University of California
 * All Rights Reserved
 *
 * Permission to use, copy, modify and distribute any part of UCSD
 * PASCAL solely authored by UC authors before June 1, 1979 for
 * educational, research and non-profit purposes, without fee, and
 * without a written agreement is hereby granted, provided that the
 * above copyright notice, this paragraph and the following three
 * paragraphs appear in all copies.
 *
 * Those desiring to incorporate UCSD PASCAL into commercial products or
 * use for commercial purposes should contact the Technology Transfer &
 * Intellectual Property Services, University of California, San Diego,
 * 9500 Gilman Drive, Mail Code 0910, La Jolla, CA 92093-0910,
 * Ph: (858) 534-5815, Fax: (858) 534-7345, E-Mail: invent@ucsd.edu.
 *
 * IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
 * FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES,
 * INCLUDING LOST PROFITS, ARISING OUT OF THE USE OF UCSD PASCAL, EVEN IF
 * THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF SUCH
 * DAMAGE.
 *
 * THE SOFTWARE PROVIDED HEREIN IS ON AN "AS IS" BASIS, AND THE UNIVERSITY
 * OF CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT,
 * UPDATES, ENHANCEMENTS, OR MODIFICATIONS. THE UNIVERSITY OF CALIFORNIA
 * MAKES NO REPRESENTATIONS AND EXTENDS NO WARRANTIES OF ANY KIND,
 * EITHER IMPLIED OR EXPRESS, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
 * WARRANTIES OF MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR
 * THAT THE USE OF UCSD PASCAL WILL NOT INFRINGE ANY PATENT, TRADEMARK OR
 * OTHER RIGHTS.
 *
 * Case Number: SD1991-807 (2006)
 * http://invent.ucsd.edu/technology/cases/1995-prior/SD1991-807.shtml
 *)

PROGRAM DUPLICATEDIRECTORYCOPIER;

  (*)  This utility will copy the duplicate directory  (*)
  (*)  in blocks 6 - 9 into the standard directory in  (*)
  (*)  blocks 2 - 5. It will also mark DLASTBLK as 10  (*)
  (*)  so as to maintain the duplicate directory.      (*)


  CONST
        DIRBLOX = 4; {size of a directory in blocks}
        BLOCKBYTES = 511; {size of a block in bytes minus 1}

        FIRSTIDCHAR = 7; {location of first character in volume id}
        MAXIDLENGTH = 7;

        DUPDIRLASTBLK = 10;

  VAR
       UNITID : STRING[1];
       DRIVENUM : INTEGER;
       BUFFER : PACKED ARRAY [ 1 .. DIRBLOX, 0 .. BLOCKBYTES ] OF CHAR;
       IDLENGTH, N : INTEGER;

FUNCTION  YES: BOOLEAN;
  VAR  CH : CHAR;
BEGIN
  UNITREAD( 1, CH, 1, 0, 0 );
  WRITELN;
  YES := (CH = 'Y');
END;

BEGIN { of Duplicate Directory Copier }

  PAGE(OUTPUT);
  WRITELN;
  WRITELN( 'Duplicate Directory Copier { July 12, 1978 } ' );
  WRITELN;

  REPEAT
    WRITE( '  Enter drive # of user''s disk [4 or 5]: ' );
    READLN( UNITID );
    IF LENGTH( UNITID ) > 0 THEN
      DRIVENUM := ORD( UNITID[1] ) - ORD('0')
    ELSE
      EXIT( PROGRAM );
  UNTIL DRIVENUM IN [4,5];

  UNITREAD( DRIVENUM, BUFFER, SIZEOF(BUFFER), 6, 0 );
  IDLENGTH := ORD( BUFFER[ 1, 6 ] );

  IF (IDLENGTH > MAXIDLENGTH) OR (IDLENGTH <= 0) THEN
    WRITE( 'Illegal volume name. Do you want to copy anyway? ' )

  ELSE
  BEGIN
    WRITE( 'Are you sure you want to zap the directory of ' );
    FOR N := FIRSTIDCHAR TO (FIRSTIDCHAR - 1 + IDLENGTH) DO
      WRITE( BUFFER[ 1, N ] );
    WRITE( ': {blocks 2-5}? ' );
  END;

  IF YES THEN
  BEGIN
    IF ORD( BUFFER[ 1, 2 ] ) <> DUPDIRLASTBLK THEN
    BEGIN
      WRITELN( 'A duplicate directory has not been maintained on this disk. ' );
      WRITE( '  Are you sure you want to copy the directory? ' );
      IF NOT YES THEN
        EXIT( PROGRAM )
      ELSE
      BEGIN
        BUFFER[ 1, 2 ] := CHR( DUPDIRLASTBLK );
        BUFFER[ 1, 3 ] := CHR(0);
        UNITWRITE( DRIVENUM, BUFFER, SIZEOF(BUFFER), 6, 0 );
      END;
    END;
    UNITWRITE( DRIVENUM, BUFFER, SIZEOF(BUFFER), 2, 0 );
    WRITE( '  Directory copy is complete. ' );
  END
  ELSE
    WRITE( '  Directory copy aborted. ' );

  WRITE( 'Type <ret> to exit. ' );
  READLN;

END.
